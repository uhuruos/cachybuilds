# Maintainer: Knut Ahlers <knut at ahlers dot me>
# Contributor: Det <nimetonmaili g-mail>
# Contributors: t3ddy, Lex Rivera aka x-demon, ruario

# Check for new Linux releases in: http://googlechromereleases.blogspot.com/search/label/Stable%20updates
# or use: $ curl -s https://dl.google.com/linux/chrome/rpm/stable/x86_64/repodata/other.xml.gz | gzip -df | awk -F\" '/pkgid/{ sub(".*-","",$4); print $4": "$10 }'

pkgname=google-chrome
pkgver=20220909
pkgrel=428
pkgdesc="The popular and trusted web browser by Google (Stable Channel)"
arch=('x86_64')
url="https://www.google.com/chrome"
license=('custom:chrome')
depends=('alsa-lib' 'gtk3' 'libcups' 'libxss' 'libxtst' 'nss' 'ttf-liberation' 'xdg-utils')
optdepends=(
    'pipewire: WebRTC desktop sharing under Wayland'
    'kdialog: for file dialogs in KDE'
    'gnome-keyring: for storing passwords in GNOME keyring'
    'kwallet: for storing passwords in KWallet'
)
options=('!emptydirs' '!strip')
install="${pkgname}.install"
_channel='stable'
source=("https://dl.google.com/linux/direct/google-chrome-${_channel}_current_amd64.deb"
        'eula_text.html'
        "google-chrome-${_channel}.sh")
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

package(){
    echo "  -> Extracting the data.tar.xz..."
    bsdtar -xf data.tar.xz -C ${pkgdir}/

    echo "  -> Moving stuff in place..."
    # Launcher
    install -m755 google-chrome-${_channel}.sh ${pkgdir}/usr/bin/google-chrome-${_channel}

    # Icons
    for i in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
        install -Dm644 ${pkgdir}/opt/google/chrome/product_logo_${i/x*/}.png \
            ${pkgdir}/usr/share/icons/hicolor/${i}/apps/google-chrome.png
    done

    # License
    install -Dm644 eula_text.html ${pkgdir}/usr/share/licenses/google-chrome/eula_text.html
    install -Dm644 ${pkgdir}/opt/google/chrome/WidevineCdm/LICENSE ${pkgdir}/usr/share/licenses/google-chrome-${_channel}/WidevineCdm-LICENSE.txt

    echo "  -> Fixing Chrome desktop entry..."
    sed -i \
        -e "/Exec=/i\StartupWMClass=Google-chrome" \
        -e "s/x-scheme-handler\/ftp;\?//g" \
        ${pkgdir}/usr/share/applications/google-chrome.desktop

    echo "  -> Removing Debian Cron job, duplicate product logos and menu directory..."
    rm -r \
        ${pkgdir}/etc/cron.daily/ \
        ${pkgdir}/opt/google/chrome/cron/ \
        ${pkgdir}/opt/google/chrome/product_logo_*.* \
        ${pkgdir}/usr/share/menu/
}
